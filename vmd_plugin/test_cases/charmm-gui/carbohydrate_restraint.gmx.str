* DIHEDRAL RESTRAINT POTENTIALS FOR CARBOHYDRATES
*

coor stat sele all end
set natom = ?nsel

define XXX sele ( resn *GLC* .or. resn *GAL*  .or. resn *ALT .or. resn *ALL .or. resn *GUL .or. -
                  resn *IDO* .or. resn *MAN   .or. resn *TAL .or. resn *XYL .or. resn *FUC .or. -
                  resn *RHM  .or. resn *NE5AC .or. resn HEP* .or. resn KDO ) .and. -
                ( chem OC3C61 ) show end

set cntmax = ?nsel
if cntmax .eq. 0 goto skipcarb

define JUNK sele segid ?selsegi .and. resid ?selresi end
set iatom = ?selatom

set icnt = 1
label DOCARB

   if iatom .gt. @natom goto skipcarb

   define YYY sele XXX .and. bynu @iatom:@natom end
   set segid = ?selsegi
   set resid = ?selresi
   set resn  = ?selresn

   define JUNK sele segid @segid .and. resid @resid show end
   set iatom = ?selatom
   set nresatom = ?nsel

   set issialic = no
   set iskdo    = no
   if resn .eq. ANE5AC set issialic = yes
   if resn .eq. BNE5AC set issialic = yes
   if resn .eq. KDO    set iskdo    = yes

!
! Sialic acid chair conformation
!

   if issialic .eq. yes then

      coor stat sele JUNK .and. type O6 end
      set atom0 = ?selatom
      coor stat sele JUNK .and. type C2 end
      set atom1 = ?selatom
      coor stat sele JUNK .and. type C3 end
      set atom2 = ?selatom
      coor stat sele JUNK .and. type C4 end
      set atom3 = ?selatom
      coor stat sele JUNK .and. type C5 end
      set atom4 = ?selatom
      coor stat sele JUNK .and. type C6 end
      set atom5 = ?selatom

      write title unit 53
      * CARB DIH @atom0 @atom1 @atom2 @atom3 -60.0 2.5
      * CARB DIH @atom1 @atom2 @atom3 @atom4 60.0 2.5
      * CARB DIH @atom2 @atom3 @atom4 @atom5 -60.0 2.5
      * CARB DIH @atom3 @atom4 @atom5 @atom0 60.0 2.5
      * CARB DIH @atom4 @atom5 @atom0 @atom1 -60.0 2.5
      * CARB DIH @atom5 @atom0 @atom1 @atom2 60.0 2.5
      *

      goto donext
   endif

!
! KDO chair conformation
!

   if iskdo .eq. yes then

      coor stat sele JUNK .and. type O6 end
      set atom0 = ?selatom
      coor stat sele JUNK .and. type C2 end
      set atom1 = ?selatom
      coor stat sele JUNK .and. type C3 end
      set atom2 = ?selatom
      coor stat sele JUNK .and. type C4 end
      set atom3 = ?selatom
      coor stat sele JUNK .and. type C5 end
      set atom4 = ?selatom
      coor stat sele JUNK .and. type C6 end
      set atom5 = ?selatom

      write title unit 53
      * CARB DIH @atom0 @atom1 @atom2 @atom3 60.0 2.5
      * CARB DIH @atom1 @atom2 @atom3 @atom4 -60.0 2.5
      * CARB DIH @atom2 @atom3 @atom4 @atom5 60.0 2.5
      * CARB DIH @atom3 @atom4 @atom5 @atom0 -60.0 2.5
      * CARB DIH @atom4 @atom5 @atom0 @atom1 60.0 2.5
      * CARB DIH @atom5 @atom0 @atom1 @atom2 -60.0 2.5
      *

      goto donext
   endif

!
! Other sugar chair conformation
!

   coor stat sele JUNK .and. type O5 end
   set atom0 = ?selatom
   coor stat sele JUNK .and. type C1 end
   set atom1 = ?selatom
   coor stat sele JUNK .and. type C2 end
   set atom2 = ?selatom
   coor stat sele JUNK .and. type C3 end
   set atom3 = ?selatom
   coor stat sele JUNK .and. type C4 end
   set atom4 = ?selatom
   coor stat sele JUNK .and. type C5 end
   set atom5 = ?selatom

   write title unit 53
   * CARB DIH @atom0 @atom1 @atom2 @atom3 60.0 2.5
   * CARB DIH @atom1 @atom2 @atom3 @atom4 -60.0 2.5
   * CARB DIH @atom2 @atom3 @atom4 @atom5 60.0 2.5
   * CARB DIH @atom3 @atom4 @atom5 @atom0 -60.0 2.5
   * CARB DIH @atom4 @atom5 @atom0 @atom1 60.0 2.5
   * CARB DIH @atom5 @atom0 @atom1 @atom2 -60.0 2.5
   *

   label donext

incr iatom by @nresatom
incr icnt by 1
if icnt .le. @cntmax goto DOCARB

label skipcarb